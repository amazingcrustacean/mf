// Example:  defect action on brane in Kazama-Suzuki model

option(noredefine);option(noloadLib);option(redSB);
LIB "blow.lib";
LIB "ring.lib";
int nx=2;
int ny=2;
ring RRR=(0,r),(x(1..nx),y(1..ny)),dp;
minpoly = rootofUnity(8);
poly minpolyblow = x(1)^4 + 1;

// Define W both in x- and in y-variables:

poly Wx = x(1)^4+x(2)^4;
poly Wy = y(1)^4-4*y(1)^2*y(2)+2*y(2)^2;

// Define "effective action" of D1 x Dy on Q:

matrix Q[2][2] = 0, x(1)^2 + r^4 * x(2)^2 - (1 + r^2) *r* x(1) * x(2), 
                      x(1)^2 + r^4 * x(2)^2 - ((1 + r^6)/r) * x(1) * x(2), 0;
    
// Define remaining defect:

poly Ja=y(1)-x(1)-x(2);
poly Jb=y(2)-x(1)*x(2);
poly Ea=(y(1)+x(1)+x(2))*(y(1)^2-4*y(2)+(x(1)+x(2))^2);
poly Eb=-4*x(1)^2-6*x(1)*x(2)-4*x(2)^2+2*y(2);
matrix D[4][4]=
    0,0,Ja,Eb,
    0,0,Jb,-Ea,
    Ea,Eb,0,0,
    Jb,-Ja;

// Define DQ := D x Q (and check that it is a MF of Wy):

matrix DQ = MFtensor(D,Q);
print( DQ * DQ - Wy * unitmat(8) );

list l = fuseDefects(D, Q, Wx);
matrix RD = l[1]; // Reduced inflation of Y x X
matrix ep = l[2]; // Idempotent endomorphism of RD
print(ep * ep - ep); // is ep is an idempotent "on the nose"?

matrix final = mfSuspend( mfSplitIdempotent(RD, ep), nx );
print(final);

// Check that this really is a MF: 

print( final * final - Wy * unitmat(4) );

/////////////////////////////////////////////////////////////////////////////////////////
// KS2: Kazama-Suzuki example 2, was in "failure"
/////////////////////////////////////////////////////////////////////////////////////////

// WARNING!!!!! Before running, set useGaussRow to 0 in mfReduce, as gauss_row blows
// chunks on this example

option(noredefine);option(noloadLib);option(redSB);
LIB "ring.lib";
LIB "blow.lib";
int nx=2;
int ny=4;
ring RRRR=(0,r),(x(1..nx),y(1..ny)),dp;
minpoly = rootofUnity(8);
poly minpolyblow = x(1)^4 + 1;
printlevel=1;

poly Wx = x(1)^4+x(2)^4;

poly JaXdY=x(1)+x(2)-y(1);
poly JbXdY=x(1)*x(2)-y(2);
poly EaXdY=(x(1) + x(2) + y(1))*((x(1) - x(2))^2 + y(1)^2);
poly EbXdY=2*(x(1)*x(2) - 2*y(1)^2 + y(2));
matrix xDy[4][4]=
   0,0, JaXdY,JbXdY ,
   0,0, EbXdY,-EaXdY,
   EaXdY,JbXdY ,0,0,
   EbXdY,-JaXdY;

poly JaXyDonex= y(3) + y(4)*r^2 - x(1) - x(2);
poly JbXyDonex=y(3)*y(4)*r^2 - x(1)*x(2);
poly EaXyDonex=(x(1) + y(3) + x(2) + y(4)*r^2)*((x(1) + x(2))^2 + (y(3) - y(4)*r^2)^2);
poly EbXyDonex=-4*x(1)^2 - 6*x(1)*x(2) - 4*x(2)^2 + 2*y(3)*y(4)*r^2;
matrix XyDonex[4][4]=
   0,0, JaXyDonex,JbXyDonex ,
   0,0, EbXyDonex,-EaXyDonex,
   EaXyDonex, JbXyDonex,0,0,
   EbXyDonex,-JaXyDonex;

list lXyDonexxDy = fuseDefects(XyDonex,xDy, Wx);
matrix RXyDonexxDy = lXyDonexxDy[1]; // Reduced inflation of Y x X
matrix epXyDonexxDy = lXyDonexxDy[2]; // Idempotent endomorphism of RXyDonexxDy

// Unfortunately the (32x32)-matrix epXyDonexxDy is not an idempotent on
// the nose (but only up to homotopy):
print(epXyDonexxDy * epXyDonexxDy == epXyDonexxDy);

// Strictify the idempotent. ep is homotopic to epXyDonexxDy but actually
// idempotent on the nose.
matrix ep = mfStrictifyIdempotent(RXyDonexxDy, epXyDonexxDy);
print(ep * ep == ep);

matrix final = mfSuspend( mfSplitIdempotent(RXyDonexxDy, ep), nx );
print(final);