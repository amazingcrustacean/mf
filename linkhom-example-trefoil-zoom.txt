option(noredefine);option(noloadLib);option(redSB);//option(prot);
LIB "blow.lib";
LIB "linkhom.lib";
LIB "matrix.lib";
LIB "control.lib";
LIB "mfweb.lib";
ring rr=0,(x(1..6)),dp;
int N = 6; // The N in sl(N) link homology
printlevel = 1;

// Define the underlying web of the trefoil
intvec edgevec(1) = 2,1,3,4;
intvec edgevec(2) = 4,3,5,6;
intvec edgevec(3) = 6,5,1,2;

// Define which vertices are overcrossings (+1) and which are undercrossings (-1):
intvec vertexParity = -1,-1,-1;

// Define the link as a list (see linkhom.lib preceding mfCompileLink)
list trefoilLink = vertexParity, list(edgevec(1),edgevec(2),edgevec(3));

// RUNNING ON LANDI SINCE 2010-09-25--08:20:
// Compute the euler characteristic, from trefoil-data
list h = mfCompileLinkAlt(trefoilLink, "./data/trefoil-N" + string(N) + "-zoom.mp", N);

list cp = KRcomplex(trefoilLink, h);

ring ss=(0,q,t),(x),dp;
list cp = imap(rr,cp);
list cohom = computeCohomology(cp);
number euler = gradedEuler(cohom,q);
euler;

// The KR homology is described by its Poincare polynomial
number poincare = gradedPoincare(cohom, q, t);
poincare;

// Running the above code with the state of things on 24/9/10 yields
// the following.

// N = 2 invariants of the trefoil
//
// Jones polynomial: -q9 + q5 + q3 + q
// KR polynomial: (q9+q5t+q3t3+qt3)/(t3) 
//
// The Jones polynomial agrees with what I calculated by hand, and
// the KR polynomial agrees with the data on the Knot Atlas.

// N = 3 invariants of the trefoil
// 
// P_3: -q14-q12+q8+2q6+q4+q2
// KR polynomial: (q14+q12+q8t+q6t3+q6t+q4t3+q2t3)/(t3)
//
// The KR polynomial (and hence P_3) agree with the ones computed
// by Gukov-Schwarz-Vafa (GSV).

// N = 4 invariants
//
// P_4: -q19-q17-q15+q11+2q9+2q7+q5+q3
// KR polynomial: (q19+q17+q15+q11t+q9t3+q9t+q7t3+q7t+q5t3+q3t3)/(t3)
//
// With the code as of 26/9/10 this goes through in just under three minutes.

// N = 5 invariants
//
// P_4: -q24-q20-q18-q16+2q12+2q10+2q8+2q6+q4
// KR polynomial: (q24+q20+q18+q16+q12t3+q12t+q10t3+q10t+q8t3+q8t+q6t3+q6t+q4t3)/(t3)
//
// With the code as of 26/9/10 this goes through in about 8.5 minutes


